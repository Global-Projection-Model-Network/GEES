

!variables
    us_rr
    "Real Discount Factor" us_rdf
    "Private Consumption" us_ch
    "Private Consumption, Point of Reference" us_ref_ch
    "Per-Capita Labor Supply" us_nh
    "Private Investment" us_ih
    "Production Capital" us_k
    "Production Capital, Point of Reference" us_ref_k
    "Production Capital Services" us_uk
    "Domestic Technology Level" us_a
    "Domestic Technology Choice: Labor" us_an, us_roc_an
    "Domestic Technology Choice: Intermediate Imports" us_am, us_roc_am
    "Domestic Technology Choice: Capital" us_ak, us_roc_ak
    "Domestic Technology Choice: Stage T-2 Output" us_ay2, us_roc_ay2
    "Shadow Value of Household Budget Constraing" us_vh
    "Price of Private Consumption" us_pch
    "Price of Private Investment" us_pih
    "Roundabout Productionb" us_z
    us_y2
    us_y1
    us_y
    us_py0
    us_py1
    us_py2
    "Nominal GDP" us_ngdp

    "Price of Domestic Production" us_py
    "Price of Production Capital" us_pk
    "Price of Production Capital Services" us_puk
    "Nominal Wage Rate" us_w, us_w0
    "Nominal Rate" us_r
    "Bank Deposits" us_bd
    
    "Price of Final Domestic Output, Rate of Change" us_roc_py
    "Price of Final Domestic Output, Rate of Change, Point of Reference" us_ref_roc_py
    us_roc_w, us_roc_pch, us_roc_ih
    "Capital Utilization Rate" us_u
    us_roc_k
    us_m
    us_pm
    us_x
    us_px


!log-variables
    !all-but


!substitutions

    us_trn := (us_pch * us_ch + us_pih * us_ih + us_kappa_1 * us_pk * us_k);
    us_curr := (us_w*us_nh*us_nf);
    us_netw := 0*us_nu_1*us_nn/(us_pk*us_k + 0*us_dg_to_ngdp*us_ngdp); 
    us_shf_curr := normpdf((log(us_pch*us_ch/($us_curr$*exp(us_shk_curr))) - log(us_xi_ch1))/us_xi_ch2)/us_xi_ch2;

    us_adj_y2 := log(us_roc_am/&us_roc_am)-log(us_roc_an/&us_roc_an) - &us_rdf*(log(us_roc_am{+1}/&us_roc_am)-log(us_roc_an{+1}/&us_roc_an)); 
    us_adj_y1 := log(us_roc_ak/&us_roc_ak)-log(us_roc_ay2/&us_roc_ay2) - &us_rdf*(log(us_roc_ak{+1}/&us_roc_ak)-log(us_roc_ay2{+1}/&us_roc_ay2)); 
    us_adj_ih := log(us_ih/us_ih{-1}/&us_roc_ih) - us_beta*log(us_ih{+1}/us_ih/&us_roc_ih);
    us_adj_py := log(us_roc_py/us_ref_roc_py) - us_beta*log(us_roc_py{+1}/us_ref_roc_py{+1});


!parameters
    us_rho_a

    us_chi_curr, us_chi_ch, us_chi
    us_nu_0, us_nu_1
    us_xi_k, us_xi_ih
    us_upsilon_0, us_upsilon_1
    us_beta, us_beta_k, us_delta, us_gamma_n0, us_gamma_n, us_gamma_uk, us_gamma_z
    us_eta, us_rho_w
    us_kappa_1, us_kappa_2
    us_ss_roc_a
    us_ss_roc_pch
    us_xi_py, us_mu_py, us_mu_y2
    us_rho_r, us_psi_pch

    us_zeta_py
    us_xi_ch0, us_xi_ch1, us_xi_ch2

    us_phi_u

    us_xi_y1, us_xi_y2

!shocks
    
    us_shk_a
    us_shk_r
    us_shk_w
    us_shk_curr


!equations



    us_vh*us_pch*[ us_ch - us_chi*us_ref_ch ] = us_nn*(1 - us_chi*&us_ref_ch/&us_ch)     !! us_vh*us_pch*us_ch = us_nn;

    us_ref_ch = us_chi_curr*(us_w*us_nh*us_nf/us_pch) + us_chi_ch*us_ch{-1};
    
    us_vh = us_beta*us_vh{+1}*us_r + $us_netw$;

    us_rdf = us_beta*us_vh{+1}*us_pch{+1}/(us_vh*us_pch);
    us_rr = us_r * us_pch / us_pch{+1};
    
    
    






    us_vh * us_w0 = 1;

    log(us_w/us_pch)         = us_rho_w * log(us_w{-1}*&us_roc_w/us_pch{-1}/&us_roc_pch)         + (1-us_rho_w) * log(us_w0/us_pch)         + us_shk_w         !! us_w = us_w0;

    us_bd = us_kappa_2 * $us_trn$;





    us_roc_k = us_k/us_k{-1};

    us_vh*us_pk*(1 + us_xi_k*log(us_k/us_ref_k))         = us_vh*us_puk + us_beta_k*us_beta*us_vh{+1}*(1-us_delta)*us_pk{+1}         + $us_netw$*us_pk;

    us_ref_k = us_k{+1}; 

    us_pk = us_pih*[ 1 + us_xi_ih*($us_adj_ih$) ]     !! us_pk = us_pih;

    us_roc_ih = us_ih/us_ih{-1};

    us_k = (1-us_delta)*us_k{-1} + us_ih;

    us_uk = us_u * us_k !! us_uk = us_k;
    us_u = (&us_py / &us_puk * us_puk / us_py)^us_upsilon_1 !! us_u = 1;




    !if open
        us_y2 = us_an * (us_nh - us_gamma_n0*&us_nh) * us_nf;
        us_y2 = us_am * us_m;
        us_py2 = us_mu_y2 * us_w/us_an + us_pm/us_am;

        us_gamma_n*us_pm/us_am - us_mu_y2*(1-us_gamma_n)*us_w/us_an = us_py2*us_xi_y2*($us_adj_y2$)         !! us_gamma_n*us_pm/us_am - us_mu_y2*(1-us_gamma_n)*us_w/us_an = 0;

        us_am^(1-us_gamma_n) * us_an^us_gamma_n = us_a^us_gamma_n;
    !else
        us_y2 = us_an * (us_nh - us_gamma_n0*&us_nh) * us_nf;
        us_py2 = us_mu_y2 * us_w / us_an;
        us_an = us_a;
        us_am = 1;
        us_m = 1;
    !end
    
    difflog(us_a) = us_rho_a*difflog(us_a{-1}) + (1-us_rho_a)*(log(us_ss_roc_a) + us_shk_a)     !! us_a = us_a{-1}*us_ss_roc_a;






    us_y1 = us_ak * us_uk;
    us_y1 = us_ay2 * us_y2;
    us_py1 = us_puk/us_ak + us_py2/us_ay2;

    (1-us_gamma_uk)*us_puk/us_ak - us_gamma_uk*us_py2/us_ay2 = us_py1*us_xi_y1*($us_adj_y1$)     !! (1-us_gamma_uk)*us_puk/us_ak - us_gamma_uk*us_py2/us_ay2;

    us_ak^us_gamma_uk * us_ay2^(1-us_gamma_uk) = 1;

    us_roc_am = us_am/us_am{-1};
    us_roc_an = us_an/us_an{-1};

    us_roc_ak = us_ak/us_ak{-1};
    us_roc_ay2 = us_ay2/us_ay2{-1};






    us_y + us_z = us_y1^(1-us_gamma_z) * (&us_y1/&us_z*us_z)^us_gamma_z;
    (1-us_gamma_z) * us_py0 * (us_y + us_z) = us_py1 * us_y1;
    us_gamma_z * us_py0 * (us_y + us_z) = us_py * us_z;






    us_mu_py*us_py0 = us_py*[1 + (us_mu_py-1)*us_xi_py*($us_adj_py$)]     !! us_py = us_mu_py * us_py0;

    us_ref_roc_py = us_roc_py{-1}^us_zeta_py * &us_roc_py^(1-us_zeta_py);
    
    us_y = us_ch + us_cg + us_ih !if open; + us_x !end;

    us_pch = us_py;
    us_pih = us_py;
    us_pm = us_py;

    us_px = us_pm;
    us_m = us_x;




    "Monetary Policy Reaction Function"
    log(us_r)         = us_rho_r * log(us_r{-1})         + (1-us_rho_r) * [ log(&us_r) + us_psi_pch * (log(us_roc_pch{+1}) - log(us_ss_roc_pch)) ]         + us_shk_r     !! us_pch = us_pch{-1} * us_ss_roc_pch;
    
        
        
        






    "Nominal GDP"
    us_ngdp = us_pch*us_ch + us_pih*us_ih + us_pcg*us_cg;

    !for 
        us_cg
        us_w, us_pch, us_py
    !do
        us_roc_? = ? / ?{-1};
    !end




!postprocessor

    us_nch_to_ngdp = us_pch * us_ch / us_ngdp;
    us_nih_to_ngdp = us_pih * us_ih / us_ngdp;






!variables
    "Government Debt Interest Rate" us_rg
    "Price of Government Consumption" us_pcg
    "Government Consumption" us_cg
    "Government Debt to GDP Ratio" us_dg_to_ngdp
    "Net Lump-Sum Tax Rate on Wealthy Consumers" us_txl1
    "Net Lump-Sum Tax Rate on Poor Consumers" us_txl2
    "Government Consumption, Rate of Change" us_roc_cg


!log-variables
    !all-but
    us_txl1, us_txl2, us_dg_to_ngdp
    


!parameters
    us_ss_dg_to_ngdp
    us_ss_ncg_to_ngdp
    us_tau_cg
    us_tau_txl1
    us_rho_cg
    us_rho_txl2
    us_rho_txl1

!shocks
    us_shk_cg
    us_shk_txl1
    us_shk_txl2


!equations

    us_pcg = us_py;
    us_rg = us_r;

    us_dg_to_ngdp*us_ngdp         = us_rg{-1}*us_dg_to_ngdp{-1}*us_ngdp{-1}         + us_pcg*us_cg - (us_txl1 + us_txl2)*us_pch*us_ch;

    log(us_cg)         = us_rho_cg*log(us_cg{-1}*&us_roc_cg)         + (1-us_rho_cg)*log(us_ss_ncg_to_ngdp*us_ngdp/us_pcg)         - us_tau_cg*(us_dg_to_ngdp{+1} - us_ss_dg_to_ngdp)         + us_shk_cg     !! us_pcg*us_cg = us_ss_ncg_to_ngdp*us_ngdp;

    us_txl1         = us_rho_txl1*us_txl1{-1}         + (1-us_rho_txl1)*&us_txl1         + us_tau_txl1*(us_dg_to_ngdp{+1} - us_ss_dg_to_ngdp)         + us_shk_txl1     !! us_dg_to_ngdp = us_ss_dg_to_ngdp;

    us_txl2 = us_rho_txl2*us_txl2{-1} + us_shk_txl2 !! us_txl2 = 0;






!variables
    "Total Population" us_nn
    "Working Age Population" us_nw
    "Labor Force" us_nf


!parameters
    "S/S Population, Rate of Change" us_ss_roc_nn
    "S/S Share of Working Age Population in Total Population" us_ss_nw_to_nn
    "S/S Labor Participation Rate" us_ss_nf_to_nw

    us_rho_nn
    us_rho_nw
    us_rho_nf
    

!shocks
    us_shk_nn


!equations

    "Total Population"
    log(us_nn/us_nn{-1})         = us_rho_nn * log(us_nn{-1}/us_nn{-2})         + (1-us_rho_nn) * log(us_ss_roc_nn)         + (1-us_rho_nn) * us_shk_nn     !! us_nn = us_nn{-1}*us_ss_roc_nn;


    "Share of Working Age Population in Total Population"
    us_nw/us_nn         = us_rho_nw * us_nw{-1}/us_nn{-1}         + (1-us_rho_nw) * us_ss_nw_to_nn     !! us_nw = us_ss_nw_to_nn * us_nn;


    "Labor Participation Rate"
    us_nf/us_nw         = us_rho_nf * us_nf{-1}/us_nw{-1}         + (1-us_rho_nf) * us_ss_nf_to_nw     !! us_nf = us_ss_nf_to_nw * us_nw;


